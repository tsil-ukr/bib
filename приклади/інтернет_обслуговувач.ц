взяти визначення турбо;
взяти визначення Друк;

дія обробник_закриття_інтернет_звʼязку(турбіна: адреса<турбо::Турбіна>, інтернет_звʼязок: адреса<турбо::ІнтернетЗвʼязок>, аргумент: невідома_адреса) {
  біб::надрукувати_ю8(ю8"закрито звʼязок");
}

дія обробник_запису_інтернет_звʼязку(турбіна: адреса<турбо::Турбіна>, інтернет_звʼязок: адреса<турбо::ІнтернетЗвʼязок>, аргумент_інтернет_звʼязку: невідома_адреса, аргумент_запису: невідома_адреса, розмір: позитивне, дані: памʼять<п8>) {
  біб::надрукувати_ю8(ю8"записано");
  біб::звільнити_памʼять(турбіна.виділяч, дані);
}

дія обробник_даних_інтернет_звʼязку(турбіна: адреса<турбо::Турбіна>, інтернет_звʼязок: адреса<турбо::ІнтернетЗвʼязок>, аргумент: невідома_адреса, розмір: позитивне, дані: памʼять<п8>) {
  біб::вивести_ю8(ю8"дані звʼязку ");
  біб::вивести_ю8(ю8 { розмір, дані });

  турбо::записати_в_інтернет_звʼязок(турбіна, інтернет_звʼязок, розмір, дані, обробник_запису_інтернет_звʼязку, пусто);
}

дія обробник_відключення_інтернет_звʼязку(турбіна: адреса<турбо::Турбіна>, інтернет_звʼязок: адреса<турбо::ІнтернетЗвʼязок>, аргумент: невідома_адреса, помилка: позитивне) {
  якщо помилка == 0 {
    біб::надрукувати_ю8(ю8"відключено звʼязок");
  } інакше {
    біб::надрукувати_ю8(ю8"відключено звʼязок з помилкою");
  }
}

дія обробник_підключення_клієнта(турбіна: адреса<турбо::Турбіна>, інтернет_обслуговувач: адреса<турбо::ІнтернетОбслуговувач>, інтернет_звʼязок: адреса<турбо::ІнтернетЗвʼязок>, аргумент: невідома_адреса) {
  біб::надрукувати_ю8(ю8"підключено звʼязок");
  турбо::встановити_обробники_інтернет_звʼязку(турбіна, інтернет_звʼязок, обробник_даних_інтернет_звʼязку, обробник_закриття_інтернет_звʼязку, обробник_відключення_інтернет_звʼязку, пусто);
}

дія обробник_запуску(турбіна: адреса<турбо::Турбіна>, інтернет_обслуговувач: адреса<турбо::ІнтернетОбслуговувач>, аргумент: невідома_адреса, іа: позитивне, порт: позитивне) {
  біб::надрукувати_ю8(ю8"запущено інтернет обслуговувач");
}

дія обробник_зупинки(турбіна: адреса<турбо::Турбіна>, інтернет_обслуговувач: адреса<турбо::ІнтернетОбслуговувач>, аргумент: невідома_адреса) {
  біб::надрукувати_ю8(ю8"зупинено інтернет обслуговувач");
}

дія старт(): ц32 {
  ціль виділяч = біб::отримати_системний_виділяч();

  ціль турбіна = турбо::створити_турбіну(виділяч);

  змінна іа: позитивне = 0;

  ціль результат_іа = турбо::отримати_інтернет_адресу_з_ю8(турбіна, ю8"0.0.0.0", іа::адреса);
  якщо результат_іа == ні {
    біб::надрукувати_ю8(ю8"помилка отримання інтернет адреси");
    вернути 1;
  }

  ціль успіх_запуску = турбо::запустити_інтернет_обслуговувач(турбіна, іа, 8080, обробник_запуску, обробник_підключення_клієнта, обробник_зупинки, пусто);
  якщо успіх_запуску == ні {
    біб::надрукувати_ю8(ю8"помилка запуску інтернет обслуговувача");
  }

  турбо::запустити_турбіну(турбіна, пусто, пусто, пусто);

  турбо::знищити_турбіну(турбіна);

  вернути 0;
}